[31:] T 76 S

[32:] X 0 S [ сменить на Z 0 S для отладки ]
[33:] A 69 [<power>] S [ загрузка в аккумулятор степени многочлена ]
[34:] T 1 S [ запись степени многочлена в ячейку 1 ]
[35:] A 71 [<a4>] S [ загрузка старшего коэффициента в аккумулятор ]
[36:] T 2 S [ запись старшего коэффициента в рабочую ячейку 2 ]
[37:] H 68 [<c1>] S [ запись 1 в умножающий регистр ]
[38:] V 2 S [ умножение числа из ячейки 2 на число в умножающем регистре ]
[39:] R 1 S [ сдвиг числа вправо на 2 бита, для коррекции умножения ]
[40:] T 2 L [ запись старшего коэффициента в длинную ячейку ]

[41:] A 70 [<addr>] S [ загрузка в аккумулятор адреса 1-го элемента массива ]
[42:] L 0 L [ сдвиг аккумулятора, для коррекции ]
[43:] A 54 [<r1>] S [ прибавляем код инструкции с нулевым полем адреса ]
[44:] T 54 [<r1>] S [ запись сформированной инструкции, обнуление аккумулятора ]

[loop:]
[45:] A 1 S [ загружаем в аккумулятор счётчик необработанных элементов массива ]
[46:] S 68 S [ вычитание константы = 1 ]
[47:] G 66 [<exit>] S [ if (acc < 0) end ]

[48:] T 1 S [ обновляем значение счетчика и обнуляем аккумулятор ]
[49:] H 67 [<x0>] S [ запись x0 в умножающий регистр ]
[50:] V 2 L [ умножение числа из ячейки 2 на число в умножающем регистре ]
[51:] L 1024 S [ сдвиг числа влево на 12 бит, для коррекции умножения ]
[52:] L 4 S [ сдвиг числа влево на 4 бита, для коррекции умножения ]
[53:] T 2 L [ запись результата умножения в длинную ячейку ]

[54 r1:] A 0 S [ загрузка в аккумулятор значения из ячейки 0 ]
[55:] T 4 S [ запись в рабочую ячейку 4 ]
[56:] H 68 [<c1>] S [ запись 1 в умножающий регистр ]
[57:] V 4 S [ умножение числа из ячейки 4 на число в умножающем регистре ]
[58:] R 1 S [ сдвиг числа вправо на 2 бита, для коррекции умножения ]
[59:] A 2 L [ добавление числа из "длинной" ячейки 2 в аккумулятор ]
[60:] T 2 L [ запись этого значения в "длинную" ячейку 2, обнуление аккумулятора ]

[61:] A 68 [<c1>] S [ загрузка в аккумулятор значение константы = 1 ]
[62:] L 0 L [ сдвиг аккумулятора, для коррекции ]
[63:] A 54 [<r1>] S [ прибавляем код инструкции, исполненной на предыдущем шаге ]
[64:] T 54 [<r1>] S [ записываем сформированную инструкцию в память ]

[65:] E 45 [<loop>] S [ повторяем все операции; аккумулятор обнулен ]

[66 exit:] Z 0 S [ точка остановки, завершение программы ]

[67:] P 1 S [ x0 = 2 - значение, по которому мы вычисляем значение многочлена ]
[68 const 1:] P 0 L [ c1 = 1 ]
[69 power:] P 2 S [ степень многочлена ]
[70 addr:] P 36 S [ адрес 1-го элемента массива ]
[71:] P 0 L [ a4 = 1 ]
[72:] P 1 L [ a3 = 3 ]
[73:] P 2 L [ a2 = 5 ]
[74:] P 3 L [ a1 = 7 ]
[75:] P 4 L [ a0 = 9 ]
