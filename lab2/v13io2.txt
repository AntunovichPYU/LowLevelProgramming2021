T 56 K

G    K
[0:] A 3 F [ пролог: формирование кода инструкции возврата в Acc]
[1:] T 31 [<ret>] @ [ пролог: запись инструкции возврата ]
[2:] H 33 [<c1>] @ [ запись 1 в умножающий регистр ]
[3:] V 2 F [ умножение числа из ячейки 2 на число в умножающем регистре ]
[4:] R 1 F [ сдвиг числа вправо на 2 бита, для коррекции умножения ]
[5:] T 2 D [ запись старшего коэффициента в длинную ячейку ]

[6:] A 1 [<addr>] F [ загрузка в аккумулятор адреса 1-го элемента массива ]
[7:] A 18 [<r1>] @ [ прибавляем код инструкции с нулевым полем адреса ]
[8:] T 18 @ [ запись сформированной инструкции, обнуление аккумулятора ]

[loop:]
[9:] A 0 F [ загружаем в аккумулятор счётчик необработанных элементов массива ]
[10:] S 33 @ [ вычитание константы = 1 ]
[11:] G 30 [<exit>] @ [ if (acc < 0) end ]

[12:] T 0 F [ обновляем значение счетчика и обнуляем аккумулятор ]
[13:] H 32 @ [ запись x0 в умножающий регистр ]
[14:] V 2 D [ умножение числа из ячейки 2 на число в умножающем регистре ]
[15:] L 1024 F [ сдвиг числа влево на 12 бит, для коррекции умножения ]
[16:] L 4 F [ сдвиг числа влево на 4 бита, для коррекции умножения ]
[17:] T 2 D [ запись результата умножения в длинную ячейку ]

[18 r1:] A 0 F [ загрузка в аккумулятор значения из ячейки 0 ]
[19:] T 4 F [ запись в рабочую ячейку 4 ]
[20:] H 33 @ [ запись 1 в умножающий регистр ]
[21:] V 4 F [ умножение числа из ячейки 4 на число в умножающем регистре ]
[22:] R 1 F [ сдвиг числа вправо на 2 бита, для коррекции умножения ]
[23:] A 2 D [ добавление числа из "длинной" ячейки 2 в аккумулятор ]
[24:] T 2 D [ запись этого значения в "длинную" ячейку 2, обнуление аккумулятора ]

[25:] A 33 @ [ загрузка в аккумулятор значение константы = 1 ]
[26:] L 0 D
[27:] A 18 [<r1>] @ [ прибавляем код инструкции, исполненной на предыдущем шаге ]
[28:] T 18 [<r1>] @ [ записываем сформированную инструкцию в память ]

[29:] E 9 [<loop>] @ [ повторяем все операции; аккумулятор обнулен ]

[30 exit:] T 0 F [ точка остановки, завершение программы ]

[31 ret:] E 0 F [ эпилог: инструкция возврата из подпрограммы ]

[32:] P 1 F [ x0 - значение, по которому мы вычисляем значение многочлена ]
[33 const 1:] P 0 D [ c1 = 1 ]

G    K
[0:] X 0 F
[1:] A 10 [<power>] @ [ загрузка в аккумулятор степени многочлена ]
[2:] T 0 F [ запись степени многочлена в ячейку 0 ]
[3:] A 11 [<addr>] @ [ загрузка в аккумулятор адреса массива ]
[4:] T 1 F [ запись адреса массива в ячейку 1]
[5:] A 12 [<a4>] @ [ загрузка старшего коэффициента в аккумулятор ]
[6:] T 2 F [ запись старшего коэффициента в рабочую ячейку 2 ]

[7:] A 7 @      [\Запуск]
[8:] G 56 F     [/подпрограммы]
[9:] Z 0 F      [останов]

[10 power:] P 2 F [ степень многочлена ]
[11 addr:] P 13 @ [ адрес массива ]
[12:] P 0 D [ a4 = 1 ]
[array:]
[13:] P 1 D [ a3 = 3 ]
[14:] P 2 D [ a2 = 5 ]
[15:] P 3 D [ a1 = 7 ]
[16:] P 4 D [ a0 = 9 ]
EZ PF
